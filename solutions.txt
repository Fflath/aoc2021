## D1
d1←⍎⍤1⊢↑⊃⎕NGET'in1.txt'1
+/{</⍵}⌺2⊢d1
+/{</⍵}⌺2⊢1↓{+/⍵}⌺3⊢d1

## D2
d2←⊃⎕NGET'in2.txt'1
{(0⌷[1]⍵) {⍺, +/⍵}⌸(⍎¨1⌷[1]⍵)}⊢⌺(⍪2 2)⊢'[a-z]+|\d'⎕S'&' ⊢d2

forward←{⍺+(⍵,(⍵×2↓⍺),0)}
down←{⍺+(0 0 ⍵)}
up←{⍺-(0 0 ⍵)}
step←{f s←⍵ ⋄ ⍺(⍎f)(⍎s)}
run←{⍺←0 0 0 ⋄ 0=≢⍵:⍺ ⋄ (⍺ step⊃⍵)∇ 1↓⍵}
×/2↑run {⊂⍵}⌺(⍪2 2)⊢'[a-z]+|\d'⎕S'&' ⊢d2

## D3
d3←⊃⎕nget'in3.txt'1

gamma←{2⊥,↑{s←+/⍵ ⋄ s>(⍴⍵)-s}¨(⍎¨)¨↓⍉↑⍵}
epsilon←{2⊥,↑{s←+/⍵ ⋄ s<(⍴⍵)-s}¨(⍎¨)¨↓⍉↑⍵}
{(gamma ⍵) × (epsilon ⍵)}d3


t←{
    pos arr←⍵ ⋄ op←⍺⍺
    pos⊃{s←+/⍵ ⋄ s op(⍴⍵)-s}¨(⍎¨)¨↓⍉↑arr
}
mcb←{⍺⊃{s←+/⍵ ⋄ s≥(⍴⍵)-s}¨(⍎¨)¨↓⍉↑⍵}
lcb←{⍺⊃{s←+/⍵ ⋄ s<(⍴⍵)-s}¨(⍎¨)¨↓⍉↑⍵}
co2←
{
⍺←0
1=⍴⍵:2⊥⍎¨⊃⍵
ns←(⊂⍺{⍸(⍕⍺ lcb ⍵)=(⍺⌷[1]↑⍵)}⍵)⌷[0]⍵
(⍺+1)∇ ns
}

oxy←
{
⍺←0
1=⍴⍵:2⊥⍎¨⊃⍵
ns←(⊂⍺{⍸(⍕⍺ mcb ⍵)=(⍺⌷[1]↑⍵)}⍵)⌷[0]⍵
(⍺+1)∇ ns
}

## D4
n←⍎⊃f←⊃⎕nget'in4.txt'1
b←↑¨↓⍎¨↑1↓¨{⍵⊂⍨(≢⍵)⍴6↑1}1↓f
test ← {∨/(∧/[0]⍵),(∧/[1]⍵)}
n{nn←⍵∨b=⊃⍺⋄∨/test¨nn:(⊃⍺)×+/,⊃{(~⍵/nn)×⍵/b}(test¨nn)⋄(1↓⍺)∇nn}(⍴b)⍴0

## D5
d5←⊃⎕nget'in5.txt'1
⊃⍴⊃(//)↓⍉{(1<≢⍵) ⍺}⌸↑,/{↓⍉↑{⍵(⍴¨)⍨⌈/⊃∘⍴¨⍵}⍵}¨{f s←⍵ ⋄ f iotag s}¨¨{⍵/⍨{∨/=/¨⍵}¨⍵}{1 0 1 0⊂(,∘⍉2 2⍴⍵)}¨⍎¨¨'\d+'⎕S'&'¨d5
⊃⍴⊃(//)↓⍉{(1<≢⍵) ⍺}⌸↑,/{↓⍉↑{⍵(⍴¨)⍨⌈/⊃∘⍴¨⍵}⍵}¨{f s←⍵ ⋄ f iotag s}¨¨{1 0 1 0⊂(,∘⍉2 2⍴⍵)}¨⍎¨¨'\d+'⎕S'&'¨d5


## D6
⍎d6←⊃⊃⎕nget'in6.txt'1
⊃⍴({{(8⍴⍨+/⍵),(6⍴⍨+/⍵),(next/⍨~⍵)}¯1=next←⍵-1}⍣80)d6
+/1⌷[1]({{next←⍵+⊢¯1 0⍴⍨⍴⍵⋄{⍵=0:next⋄↑(⊂8 ⍵),(⊂6 ⍵),(~¯1=0⌷[1]next)/↓next}+/(,1⌽¯1=next)/(,next)}⍵}⍣256){⍺,(≢⍵)}⌸d6


## D7

d7←⍎⊃⊃⎕nget'in7.txt'1
⌊/+/¨|{d7-⍵}¨d7
⌊/+/¨{+/⍳⍵+1}¨¨|{d7-⍵}¨⍳⌈/d7
⌊/+/⍉d7 ∘.{+/⍳1+|⍺-⍵} ⍳⌈/d7

## D8
d8←⊃⎕nget'in8.txt'1
+/,↑⊃,/,(2 4 3 7)∘.=⍴¨¨10↓¨'[a-z]+'⎕S'&'¨d8

zeromap←{s←,↑⍴¨⍵ ⋄ ⊃pos/⍨~∧/¨(⊂sixmap ⍵)=pos←pos/⍨~∧⌿(fourmap ⍵)∘.∊pos←(⍵/⍨6=s)}
onemap←{s←,↑⍴¨⍵ ⋄ ⊃⍵/⍨2=s}
twomap←{s←,↑⍴¨⍵ ⋄ ⊃pos/⍨~∨⌿∧/¨((⊂fivemap ⍵),(⊂threemap ⍵))∘.=pos←(⍵/⍨5=s)}
threemap←{s←,↑⍴¨⍵ ⋄ ⊃pos/⍨~∧/¨(⊂fivemap ⍵)=pos←pos/⍨5=+⌿(ninemap ⍵)∘.∊pos←(⍵/⍨5=s)}
fourmap←{s←,↑⍴¨⍵ ⋄ ⊃⍵/⍨4=s}
fivemap←{s←,↑⍴¨⍵ ⋄ ⊃pos/⍨5=+⌿(sixmap ⍵)∘.∊pos←(⍵/⍨5=s)}
sixmap←{s←,↑⍴¨⍵ ⋄ ⊃(⍵/⍨6=s)/⍨~∧⌿(onemap ⍵)∘.∊(⍵/⍨6=s)}
sevenmap←{s←,↑⍴¨⍵ ⋄ ⊃⍵/⍨3=s}
eightmap←{s←,↑⍴¨⍵ ⋄ ⊃⍵/⍨7=s}
ninemap←{s←,↑⍴¨⍵ ⋄ ⊃pos/⍨~∨⌿∧/¨((⊂sixmap ⍵),⊂zeromap ⍵)∘.=pos←(⍵/⍨6=s)}
dmap←{(⊂zeromap ⍵),(⊂onemap ⍵),(⊂twomap ⍵),(⊂threemap ⍵),(⊂fourmap ⍵),(⊂fivemap ⍵),(⊂sixmap ⍵),(⊂sevenmap ⍵),(⊂eightmap ⍵),(⊂ninemap ⍵)}
+/10⊥¨{a b←⍵⋄(dmap a)⍳b}¨{(1 0 0 0 0 0 0 0 0 0 1 0 0 0)⊂⍵}¨{⍵[⍋⍵]}¨¨'[a-z]+'⎕S'&'¨d8

## D9
d9←⊃⎕nget'in9.txt'1
+/⍣2{⍵+b∧⍵}⊃4=+/(⊂b)<{((¯1 1)⊖¨⍵),((¯1 1)⌽¨⍵)}2⍴⊂b←1⊖↑(2⍴(⊂10⍴⍨⍴⊃b)),b←{10,⍵,10}¨⍎¨¨d9
×/1↓4↑{{⍵[⍒⍵]}1⌷[1]⍵}{⍺,(≢⍵)}⌸,({(9≠b)∧⊃∨/(¯1 1 ⊖¨2⍴⊂⍵),(¯1 0 1)⌽¨3⍴⊂⍵}⍣100){(⍴⍵)⍴(,⍵)∧⍳⍴,⍵}0≠{⍵+b∧⍵}⊃4=+/(⊂b)<{((¯1 1)⊖¨⍵),((¯1 1)⌽¨⍵)}2⍴⊂b←1⊖↑(2⍴(⊂9⍴⍨⍴⊃b)),b←{9,⍵,9}¨⍎¨¨d9
